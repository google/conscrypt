plugins {
    id 'me.champeau.gradle.jmh' version '0.3.1'
}

apply plugin: 'idea'

description = 'Conscrypt: OpenJDK Benchmarks'

ext {
    // The configuration of conscrypt-openjdk to use. This configuration will contain the native artifact for
    // the current platform.
    openJdkConfiguration = normalizeClassifier(classifierFor(osName, 'x86_64'))
}

jmh {
    jmhVersion = "$jmhVersion"
    warmupIterations = 10
    iterations = 10
    fork = 1
    jvmArgs = '-server -Xms2g -Xmx2g'
    duplicateClassesStrategy = 'warn'
}

dependencies {
    compile project(path: ':conscrypt-openjdk', configuration: "$openJdkConfiguration"), libraries.guava
    jmh libraries.junit,
            libraries.netty,
            libraries.netty_tcnative,
            // JMH plugin doesn't seem to include this dependency by default. This version of the JMH
            // plugin seems to require the use of v1.12 for all other JMH dependencies, so not overriding
            // them here.
            libraries.jmh_generator_annprocess
}

// Running benchmarks in IntelliJ seems broken without this.
// See https://github.com/melix/jmh-gradle-plugin/issues/39
idea.module {
    scopes.PROVIDED.plus += [ configurations.jmh ]
}

static classifierFor(osName, archName) {
    return "${osName}-${archName}"
}

static normalizeClassifier(classifier) {
    return classifier.replaceAll("-", "_")
}