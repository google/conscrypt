plugins {
    id 'me.champeau.gradle.jmh' version '0.3.1'
}

apply plugin: 'idea'

description = 'Conscrypt: OpenJDK Benchmarks'

ext {
    // The configuration of conscrypt-openjdk to use. This configuration will contain the native artifact for
    // the current platform.
    openJdkConfiguration = normalizeClassifier(classifierFor(osName, 'x86_64'))
}

jmh {
    jmhVersion = "$jmhVersion"
    warmupIterations = 10
    iterations = 10
    fork = 1
    jvmArgs = '-server -Xms2g -Xmx2g'
    duplicateClassesStrategy = 'warn'
}

configurations {
    jmhGenerators
}

dependencies {
    compile project(path: ':conscrypt-openjdk', configuration: "$openJdkConfiguration"),
            project(':conscrypt-testing'),
            libraries.guava,
            libraries.junit,
            libraries.netty_handler,
            libraries.netty_tcnative

    jmh libraries.jmh_core

    jmhGenerators libraries.jmh_generator_asm,
            libraries.jmh_generator_bytecode,
            libraries.jmh_generator_reflection,
            libraries.jmh_generator_annprocess
}

// Running benchmarks in IntelliJ seems broken without this.
// See https://github.com/melix/jmh-gradle-plugin/issues/39
idea.module {
    scopes.PROVIDED.plus += [ configurations.jmh, configurations.jmhGenerators ]
}

static classifierFor(osName, archName) {
    return "${osName}-${archName}"
}

static normalizeClassifier(classifier) {
    return classifier.replaceAll("-", "_")
}