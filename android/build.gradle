buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libraries.android_tools
    }
}

plugins {
    id 'digital.wup.android-maven-publish' version '3.6.2'
}

description = 'Conscrypt: Android'

ext {
    androidHome = "$System.env.ANDROID_HOME"
    androidSdkInstalled = file("$androidHome").exists()
    androidVersionCode = 1
    androidVersionName = "$version"
    androidMinSdkVersion = 19
    androidTargetSdkVersion = 26
    androidNdkVersion = '25.2.9519653'
    androidCmakeVersion = '3.22.1'
}

if (androidSdkInstalled) {
    apply plugin: 'com.android.library'

    android {
        namespace "org.conscrypt"

        compileSdkVersion androidTargetSdkVersion
        ndkVersion androidNdkVersion

        compileOptions {
            sourceCompatibility androidMinJavaVersion
            targetCompatibility androidMinJavaVersion
        }

        defaultConfig {
            minSdkVersion androidMinSdkVersion
            targetSdkVersion androidTargetSdkVersion
            versionCode androidVersionCode
            versionName androidVersionName

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

            consumerProguardFiles 'proguard-rules.pro'

            externalNativeBuild {
                cmake {
                    arguments '-DANDROID=True',
                            '-DANDROID_STL=c++_static',
                            "-DBORINGSSL_HOME=$boringsslHome",
                            "-DCMAKE_CXX_STANDARD=17"
                    cFlags '-fvisibility=hidden',
                            '-DBORINGSSL_SHARED_LIBRARY',
                            '-DBORINGSSL_IMPLEMENTATION',
                            '-DOPENSSL_SMALL',
                            '-D_XOPEN_SOURCE=700',
                            '-Wno-unused-parameter'
                    targets 'conscrypt_jni'
                }
            }
            ndk {
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        sourceSets.main {
            java {
                srcDirs = [ "src/main/java" ]
            }
            resources {
                srcDirs += "build/generated/resources"
            }
            project(':conscrypt-common').sourceSets.main
        }
        externalNativeBuild {
            cmake {
                path 'CMakeLists.txt'
                version androidCmakeVersion
            }
        }
        lintOptions {
            lintConfig file('lint.xml')
        }
    }

    configurations {
        publicApiDocs
    }

    preBuild {
        dependsOn generateProperties
    }

    dependencies {
        implementation project(':conscrypt-common')

        publicApiDocs project(':conscrypt-api-doclet')

        androidTestImplementation('androidx.test.espresso:espresso-core:3.1.1', {
            exclude module: 'support-annotations'
            exclude module: 'support-v4'
            exclude module: 'support-v13'
            exclude module: 'recyclerview-v7'
            exclude module: 'appcompat-v7'
            exclude module: 'design'
        })

        compileOnly project(':conscrypt-android-stub'),
                project(':conscrypt-constants')
    }

    def configureJavaDocs = tasks.register("configureJavadocs") {
        dependsOn configurations.publicApiDocs
        doLast {
            javadocs.options.docletpath = configurations.publicApiDocs.files as List
        }
    }

    def javadocs = tasks.register("javadocs", Javadoc) {
        dependsOn configureJavadocs
        dependsOn generateProperties
        source = project(':conscrypt-common').sourceSets.main.java

        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += project(':conscrypt-android-stub').sourceSets.main.output
        classpath += project(':conscrypt-constants').sourceSets.main.output
        classpath += project(':conscrypt-platform-stub').sourceSets.main.output

        // TODO(nmittler): Fix the javadoc errors.
        failOnError false
        options {
            encoding = 'UTF-8'
            links "https://docs.oracle.com/javase/8/docs/api/"
            // TODO(prb): Update doclet to Java 11.
            // doclet = "org.conscrypt.doclet.FilterDoclet"
        }
    }

    def javadocsJar = tasks.register("javadocsJar", Jar) {
        dependsOn javadocs
        archiveClassifier = 'javadoc'
        from {
            javadocs.get().destinationDir
        }
    }

    def sourcesJar = tasks.register("sourcesJar", Jar) {
        archiveClassifier = 'sources'
        from android.sourceSets.main.java.srcDirs
        from project(":conscrypt-common").sourceSets.main.java.srcDirs
    }

    apply from: "$rootDir/gradle/publishing.gradle"
    publishing.publications.maven {
        from components.android
        artifact sourcesJar.get()
        artifact javadocsJar.get()
    }
} else {
    logger.warn('Android SDK has not been detected. The Android module will not be built.')

    // Disable all tasks
    tasks.configureEach {
        it.enabled = false
    }
}
