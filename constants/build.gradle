description = 'Conscrypt: Constants'

ext {
    genDir = "${project.buildDir}/generated-sources"
}

// Needs to be binary-compatible with Android minSdkVersion.
sourceCompatibility = androidMinJavaVersion
targetCompatibility = androidMinJavaVersion

sourceSets.main {
    java {
        srcDirs = [
            "${genDir}"
        ]
    }
}

dependencies {
    compile files("${genDir}") {
        builtBy 'runGen'
    }
}

// Generate javadoc and sources JAR
artifacts {
    archives javadocJar, sourcesJar
}

model {
    components {
        // Builds exe/ which generates the content of NativeConstants.java
        gen(NativeExecutableSpec) {
            sources {
                cpp {
                    // Sources assumed to be in src/gen/cpp by default.
                    exportedHeaders {
                        srcDirs "${boringsslIncludeDir}"
                    }
                }
            }
        }
    }

    tasks {
        // Runs generateNativeConstants to create build/NativeConstants.java
        runGen(Exec) {
            def gen = $.binaries.get("genExecutable")

            dependsOn gen
            File genDir = new File("${genDir}/org/conscrypt")
            genDir.mkdirs()

            executable gen.executable.file

            doFirst {
                standardOutput = new FileOutputStream(new File(genDir, "NativeConstants.java"))
            }
            doLast {
                if (standardOutput != null) {
                    standardOutput.close();
                }
            }
        }
    }
}
