description = 'Conscrypt: Constants'

ext {
    genDir = "${project.buildDir}/generated-sources"
    // TODO(nmittler): There must be a better way to do this.
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        genConstExecutable = 'gen.exe'
    } else {
        genConstExecutable = 'gen'
    }
}

// Needs to be binary-compatible with Android minSdkVersion.
sourceCompatibility = androidMinJavaVersion
targetCompatibility = androidMinJavaVersion

sourceSets.main {
    java {
        srcDirs = [
            "${genDir}"
        ]
    }
}

dependencies {
    compile files("${genDir}") {
        builtBy 'runGen'
    }
}

// Generate javadoc and sources JAR
artifacts {
    archives javadocJar, sourcesJar
}

model {
    components {
        // Builds exe/ which generates the content of NativeConstants.java
        gen(NativeExecutableSpec) {
            sources {
                cpp {
                    // Sources assumed to be in src/gen/cpp by default.
                    exportedHeaders {
                        srcDirs "${boringsslIncludeDir}"
                    }
                }
            }
        }
    }
}

// Runs generateNativeConstants to create build/NativeConstants.java
task runGen(type:Exec)  {
    File genDir = new File("${genDir}/org/conscrypt")
    genDir.mkdirs()

    executable "${project.buildDir}/exe/gen/${genConstExecutable}"
    doFirst {
        standardOutput = new FileOutputStream(new File(genDir, "NativeConstants.java"))
    }
    doLast {
        if (standardOutput != null) {
            standardOutput.close();
        }
    }
}
runGen.dependsOn 'genExecutable'
